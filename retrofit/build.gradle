apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.vanniktech.maven.publish'

dependencies {
  api libs.okhttp

  compileOnly libs.android
  compileOnly libs.kotlinStdLib
  compileOnly libs.kotlinCoroutines

  compileOnly libs.animalSnifferAnnotations
  compileOnly libs.findBugsAnnotations

  testImplementation projects.retrofit.testHelpers
  testImplementation libs.junit
  testImplementation libs.assertj
  testImplementation libs.guava
  testImplementation libs.mockwebserver
}

jar {
  manifest {
    attributes  'Automatic-Module-Name': 'retrofit2'
  }
}

// Create a test task for each supported JDK.
(8..19).each { majorVersion ->
  // Adoptium JDK 9 cannot extract on Linux or Mac OS.
  if (majorVersion == 9) return

  def jdkTest = tasks.register("testJdk$majorVersion", Test) {
    javaLauncher = javaToolchains.launcherFor {
      languageVersion = JavaLanguageVersion.of(majorVersion)
    }

    description = "Runs the test suite on JDK $majorVersion"
    group = LifecycleBasePlugin.VERIFICATION_GROUP

    // Copy inputs from normal Test task.
    def testTask = tasks.getByName("test")
    classpath = testTask.classpath
    testClassesDirs = testTask.testClassesDirs
  }
  tasks.named("check").configure {
    dependsOn(jdkTest)
  }
}
